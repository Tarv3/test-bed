WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ "\n" }

main = _{ 
    SOI 
    ~
    includes?  
    ~
    template_output?
    ~
    (globals)?
    ~
    (templates)*
    ~
    (commands)*
    ~ 
    EOI 
}

includes = {
    "[includes]" ~ (string_whitespace)*
}

template_output = {
    "[output]" ~ string_whitespace
}

globals = {
    "[globals]" ~ globals_program
}

templates = {
    "[template." ~ ident ~ "]" ~ template_program 
}

commands = {
    "[commands" ~ ("." ~ ident)? ~ "]" ~ command_program
}

// ============ Globals ================

globals_program = {
    (global_assignment ~ ";")*
}

global_assignment = {
    (variable_load | variable_assignment)
}

variable_load = {
    ident ~ "=" ~ "load" ~ "(" ~ string_builder ~ ")"
}

// ============ Globals ================

// ============ Templates ==============

template_program = {
    (template_expr)*
}

template_expr = {
    (template ~ ";") | template_for_loop | template_if_statement
}

template_for_loop = {
    for_loop ~ "{" ~ (template_expr)* ~ "}"
}

template_if_statement = {
    if_statement ~ "{" ~ (template_expr)* ~ "}"
}

template = {
    print_var | 
    build_assignment |
    variable_assignment |
    push | 
    yield_template
}

build_assignment = {
    ident ~ "=" ~ build_object
}

yield_template = { 
    "yield" ~ yield_object
}

yield_object = {
    build_object | object
}

build_object = {
    ("(" ~ build_fn ~ ("," ~ property_assignment)* ~ ","? ~ ")") | build_fn
}

build_fn = {
    "build" ~ "(" ~ string_builder ~ "," ~ string_builder ~ ")"
}

// ============ Templates ==============


// ============= Commands ==============

command_program = {
    (command_expr)*
}

command_expr = {
    (command ~ ";") | command_for_loop | command_if_statement
    
}

command_for_loop = {
    for_loop ~ "{" ~ (command_expr)* ~ "}"
}

command_if_statement = {
    if_statement ~ "{" ~ (command_expr)* ~ "}"
}

command = {
    print_var | 
    variable_assignment | 
    push | 
    limit_spawn | 
    sleep | 
    wait_all | 
    spawn 
}


limit_spawn = {
    "limit" ~ integer
}

sleep = {
    "sleep" ~ integer
}

wait_all = {
    "wait_all" ~ (integer)?
}

spawn = {
    "spawn" ~ working_dir? ~ std_map? ~ string_builder ~ (arg_builder)*
}

working_dir = {
    "dir("
    ~
    string_builder
    ~
    ")"
}

std_map = {
    (stderr_map ~ stdout_map?) | (stdout_map ~ stderr_map?)
}

stderr_map = {
    "stderr("
    ~
    output_map
    ~
    ")"
}

stdout_map = {
    "stdout("
    ~
    output_map
    ~
    ")"
}

output_map = {
    append | print | string_builder
}

append = {
    "append(" ~ string_builder ~ ")"
}

print = { "print" }

arg_builder = {
   "{" ~ variable_access ~ "}" | string_builder 
}

// ============= Commands ==============

if_statement = {
    ("if" ~ variable_access+)
}

for_loop = {
    for_loop_combinations | for_loop_groups
}

for_loop_combinations = {
    ("for" ~ ident ~ "in" ~ iterable) | ("for" ~ ident_group ~ "in" ~ iterable_group) 
}

for_loop_groups = {
    ("for" ~ "group" ~ ident ~ "in" ~ iterable) | ("for" ~ "group" ~ ident_group ~ "in" ~ iterable_group) 
}

ident_group = {
    "(" ~ ident ~ ("," ~ ident)* ~ (",")? ~ ")"
}

iterable_group = {
    "(" ~ iterable ~ ("," ~ iterable )* ~ (",")? ~ ")"
}

iterable = {
    (range | ident)
}

range = {
    range_expr ~ ".." ~ range_expr
}

variable_assignment = {
    ident ~ variable_assign_op ~ object
}

push = {
    ident ~ "." ~ "push" ~ "(" ~ object ~ ")" 
}

list_expression = {
    "[" ~ (object ~ ("," ~ object)* ~ ","?)? ~ "]"
}

variable_assign_op = {
    (variable_assign | variable_create)
}

variable_create = {
    "="
}

variable_assign = {
    ":="
}

print_var = {
    "print" ~ "(" ~ variable_access ~ ")"
}

variable_access = {
    immediate_variable_access ~ ( "." ~ variable_access)?
}

immediate_variable_access = {
    ident ~ ("[" ~ variable_idx ~ "]")?
}

variable_clone = {
    "*" ~ variable_access
}

variable_idx = {
    integer | variable_access
}

struct_expr = {
    ("(" ~ string_builder ~ ("," ~ property_assignment)* ~ ","? ~ ")") | string_builder
}

object = {
    variable_clone | range | struct_expr | list_expression
}

property_assignment = { 
    ident ~ "=" ~ object
}

range_expr = {
    "[" ~ variable_access ~ "]" | signed_integer
}

ident = @{ ident_char ~ (ident_char | integer)* }
ident_char = { ('a'..'z') | ('A'..'Z') | "_" }

signed_integer = @{ ("-")? ~ integer }
integer = @{ ('0'..'9')+ } 

string_builder = {
    string_expr ~ ("+" ~ string_expr)*
}

string_expr = {
    "[" ~ variable_access ~ "]" | string_whitespace | string_no_whitespace
}

string_no_whitespace = @{ char_no_white_space }
char_no_white_space = {
    (!(WHITESPACE | "," | "[" | ";" | "]" | ")" | "(" | "+" | "\"") ~ ANY)+
}

string_whitespace = ${ "\"" ~ char_whitespace ~ "\""}
char_whitespace = @{ ("\\\"" | (!("\"") ~ ANY))* }

